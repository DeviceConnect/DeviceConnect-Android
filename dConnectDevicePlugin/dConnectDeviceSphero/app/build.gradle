apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.0'

    def getVersionName = { ->
        def version
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            version = ["cmd", "/c", "git describe --tags"].execute().in.text.trim()
        } else {
            version = ["sh", "-c", "git describe --tags"].execute().in.text.trim()
        }
        version = version.empty ? "2.0.0" : version
        return "${version}"
    }
    defaultConfig {
        applicationId "org.deviceconnect.android.deviceplugin.sphero"
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName getVersionName()
    }

    signingConfigs {
        releaseConfig {
            storeFile=file(project.properties.storeFile)
            storePassword=project.properties.storePassword
            keyAlias=project.properties.keyAlias
            keyPassword=project.properties.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.releaseConfig
        }
    }

    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDirs = ["src/main/jniLibs", "src/main/libs"]
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    productFlavors {
        DeviceWebApi {
            versionCode 2
            versionName "1.0.1"
        }
    }
}

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile project(':dconnect-device-plugin-sdk')
}
