apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "23.0.0"

    def getVersionName = { ->
        def version
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            version = ["cmd", "/c", "git describe --tags"].execute().in.text.trim()
        } else {
            version = ["sh", "-c", "git describe --tags"].execute().in.text.trim()
        }
        version = version.empty ? "2.0.0" : version
        return "${version}"
    }

    defaultConfig {
        applicationId "org.deviceconnect.android.deviceplugin.webrtc"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName getVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDirs = ['src/main/libs', 'src/main/jniLibs']
        }
    }
    productFlavors {
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':dconnect-device-plugin-sdk')
    compile project(':dconnect-server-nano-httpd')
    compile 'com.koushikdutta.async:androidasync:2.+'
    compile 'org.msgpack:msgpack-core:0.7.0-p7'
    compile 'com.google.code.gson:gson:2.3.1'
    compile (name:'webrtc',ext:'aar')
}

/**
 * Even though Apache HttpClient was removed from Android SDK 23, Maven repository's auto dependency
 * resolution still outputs warnings about HttpClient, and ignores HttpClient
 * (##### This status may changes in the further updates in Android SDK #####).
 *
 * Currently, this module imports HttpClient as a JAR file, and does not use Maven to resolve
 * the dependency. Also, the code below prevents dependency resolution from trying to import
 * HttpClient, thus it suppresses warnings.
 */
configurations {
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    all*.exclude group: 'commons-logging', module: 'commons-logging'
}

repositories {
    flatDir {
        dirs 'libs'
    }
}