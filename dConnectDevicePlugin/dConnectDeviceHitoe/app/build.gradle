apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    def getVersionName = { ->
        def version
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            version = ["cmd", "/c", "git describe --tags --long"].execute().in.text.trim()
        } else {
            version = ["sh", "-c", "git describe --tags --long"].execute().in.text.trim()
        }
        version = version.empty ? "2.1.0" : version
        return "${version}"
    }

    defaultConfig {
        applicationId "org.deviceconnect.android.deviceplugin.hitoe"
        minSdkVersion 18
        targetSdkVersion 23
        versionCode 1
        versionName getVersionName()
        manifestPlaceholders = [
                appIcon: "@drawable/dconnect_icon",
                appName: "Hitoe (Device Connect device plug-in)"
        ]
    }

    signingConfigs {
        releaseConfig {
            storeFile=file(project.properties.storeFile)
            storePassword=project.properties.storePassword
            keyAlias=project.properties.keyAlias
            keyPassword=project.properties.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    productFlavors {
        DeviceConnect {
            versionCode 1
            versionName "1.0.0"
            manifestPlaceholders = [
                    appIcon: "@mipmap/dconnect_icon",
                    appName: "Hitoe (Device Connect device plug-in)"
            ]
        }

        DeviceWebApi {
            versionCode 1
            versionName "1.0.0"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    appName: "Hitoe (Device Web API device plug-in)"
            ]
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':dconnect-device-plugin-sdk')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile group: 'org.achartengine', name: 'achartengine', version: '1.2.0'
}
