apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion '23.0.0'

    def getVersionName = { ->
        def version
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            version = ["cmd", "/c", "git describe --tags --long"].execute().in.text.trim()
        } else {
            version = ["sh", "-c", "git describe --tags --long"].execute().in.text.trim()
        }
        version = version.empty ? "2.1.0" : version
        return "${version}"
    }

    defaultConfig {
        applicationId "org.deviceconnect.android.deviceplugin.pebble"
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName getVersionName()
        manifestPlaceholders = [
                appIcon: "@drawable/dconnect_icon",
                appName: "@string/app_name"
        ]
    }

    signingConfigs {
        releaseConfig {
            storeFile=file(project.properties.storeFile)
            storePassword=project.properties.storePassword
            keyAlias=project.properties.keyAlias
            keyPassword=project.properties.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.releaseConfig
        }
    }
    productFlavors {
        DeviceWebApi {
            versionCode 3
            versionName "1.0.2"
            manifestPlaceholders = [
                    appIcon: "@drawable/ic_launcher",
                    appName: "@string/app_name_for_device_web_api"
            ]
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/groups/public/" }
}

configurations {
    // to avoid double inclusion of support libraries
    all*.exclude group: 'com.getpebble', module: 'pebblekit'
}

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile project(':dconnect-device-plugin-sdk')
    compile 'com.getpebble:pebblekit:3.1.0@aar'
}
