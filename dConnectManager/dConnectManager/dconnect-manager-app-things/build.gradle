apply plugin: 'com.android.application'
// Create github.properties in root project folder file with
// gpr.usr=GITHUB_USER_ID
// gpr.key=PERSONAL_ACCESS_TOKEN
def githubPropertiesFile = rootProject.file("github.properties")
def githubProperties = new Properties()
githubProperties.load(new FileInputStream(githubPropertiesFile))

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "org.deviceconnect.android.manager"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    repositories {
        maven {
            name = "DeviceConnect-Android"
            url = uri("https://maven.pkg.github.com/TakayukiHoshi1984/DeviceConnect-Android")

            credentials {
                username = System.getenv("GPR_USER") ?: githubProperties['gpr.usr']
                password = System.getenv("GPR_API_KEY") ?: githubProperties['gpr.key']
            }
        }
    }
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    androidTestImplementation('androidx.test:rules:1.1.1') {
        exclude module: 'animated-vector-drawable'
        exclude module: 'support-media-compat'
    }
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    implementation 'org.deviceconnect:dconnect-device-plugin-sdk:2.8.4.9'
    implementation project(':dconnect-manager-core')
    implementation project(':dconnect-server-nano-httpd')
    implementation project(':dconnect-device-plugin-host')
    compileOnly 'com.google.android.things:androidthings:1.0'

}
