apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.0'

    def getVersionName = { ->
        def version
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            version = ["cmd", "/c", "git describe --tags"].execute().in.text.trim()
        } else {
            version = ["sh", "-c", "git describe --tags"].execute().in.text.trim()
        }
        version = version.empty ? "2.0.0" : version
        return "${version}"
    }

    defaultConfig {
        applicationId "org.deviceconnect.android.deviceplugin.linking"
        minSdkVersion 19
        targetSdkVersion 21
        versionCode 1
        versionName getVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseConfig {
            storeFile = file(project.properties.storeFile)
            storePassword = project.properties.storePassword
            keyAlias = project.properties.keyAlias
            keyPassword = project.properties.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    configurations {
        androidTestCompile.exclude group: 'com.android.support', module: 'support-annotations'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':dconnect-device-plugin-sdk')
    compile 'com.android.support:design:23.4.0'
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
}
