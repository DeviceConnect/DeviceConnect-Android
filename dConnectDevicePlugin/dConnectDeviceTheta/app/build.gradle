apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    useLibrary 'org.apache.http.legacy'

    def getVersionName = { ->
        def version
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            version = ["cmd", "/c", "git describe --tags --long"].execute().in.text.trim()
        } else {
            version = ["sh", "-c", "git describe --tags --long"].execute().in.text.trim()
        }
        version = version.empty ? "2.1.0" : version
        return "${version}"
    }

    defaultConfig {
        applicationId "org.deviceconnect.android.deviceplugin.theta"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 7
        versionName getVersionName()
        manifestPlaceholders = [
                appIcon: "@drawable/dconnect_icon",
                appName: "@string/app_name"
        ]
    }

    signingConfigs {
        releaseConfig {
            storeFile = file(project.properties.storeFile)
            storePassword = project.properties.storePassword
            keyAlias = project.properties.keyAlias
            keyPassword = project.properties.keyPassword
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    flavorDimensions "DeviceWebApi"
    productFlavors {
        DeviceWebApi {
            versionCode 20
            versionName "2.3.0"
            manifestPlaceholders = [
                    appIcon: "@drawable/dconnect_icon",
                    appName: "@string/app_name_for_device_web_api"
            ]
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

repositories {
    maven { url 'https://raw.githubusercontent.com/DeviceConnect/DeviceConnect-Android/master/dConnectSDK/dConnectSDKForAndroid/repository/' }
    maven { url 'https://raw.githubusercontent.com/DeviceConnect/DeviceConnect-Android/master/dConnectDevicePlugin/dConnectDevicePluginSDK/repository/' }
    maven { url 'file://' + rootDir + '/../../dConnectSDK/dConnectSDKForAndroid/repository/' }
    maven { url 'file://' + rootDir + '/../../dConnectDevicePlugin/dConnectDevicePluginSDK/repository/' }
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation 'org.deviceconnect:dconnect-device-plugin-sdk:2.8.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.burgstaller:okhttp-digest:1.16'
}
