apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    def getVersionName = { ->
        def version
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            version = ["cmd", "/c", "git describe --tags --long"].execute().in.text.trim()
        } else {
            version = ["sh", "-c", "git describe --tags --long"].execute().in.text.trim()
        }
        version = version.empty ? "2.1.0" : version
        return "${version}"
    }

    defaultConfig {
        applicationId "org.deviceconnect.android.deviceplugin.hitoe"
        minSdkVersion 18
        targetSdkVersion 23
        versionCode 1
        versionName getVersionName()
        manifestPlaceholders = [
                appIcon: "@drawable/dconnect_icon",
                appName: "@string/app_name"
        ]
    }

    signingConfigs {
        releaseConfig {
            storeFile=file(project.properties.storeFile)
            storePassword=project.properties.storePassword
            keyAlias=project.properties.keyAlias
            keyPassword=project.properties.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    productFlavors {
        DeviceConnect {
            versionCode 4
            versionName "1.0.3"
            manifestPlaceholders = [
                    appIcon: "@mipmap/dconnect_icon",
                    appName: "@string/app_name"
            ]
        }

        DeviceWebApi {
            versionCode 4
            versionName "1.0.3"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    appName: "@string/app_name_for_device_web_api"
            ]
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':dconnect-device-plugin-sdk')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile group: 'org.achartengine', name: 'achartengine', version: '1.2.0'
}
