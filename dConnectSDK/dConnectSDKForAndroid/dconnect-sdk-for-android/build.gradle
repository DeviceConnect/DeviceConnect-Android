apply plugin: 'com.android.library'

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile 'com.android.support:support-v4:23.0.0'
    compile 'org.apache.httpcomponents:httpmime:4.5'
    compile 'org.apache.httpcomponents:httpcore:4.4.1'
    compile 'org.apache.james:apache-mime4j-core:0.7.2'
    compile 'commons-codec:commons-codec:1.9'
}

/**
 * Even though Apache HttpClient was removed from Android SDK 23, Maven repository's auto dependency
 * resolution still outputs warnings about HttpClient, and ignores HttpClient
 * (##### This status may changes in the further updates in Android SDK #####).
 *
 * Currently, this module imports HttpClient as a JAR file, and does not use Maven to resolve
 * the dependency. Also, the code below prevents dependency resolution from trying to import
 * HttpClient, thus it suppresses warnings.
 */
configurations {
    all*.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    all*.exclude group: 'commons-logging', module: 'commons-logging'
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}

project.ext {
    if (android.hasProperty('applicationVariants')) {
        androidVariants = android.applicationVariants;
    } else {
        androidVariants = android.libraryVariants;
    }
}

project.androidVariants.all { variant ->
    task("generateJavadocForSDK", type: Javadoc, overwrite: true) {
        title = "Device Connect SDK for Android"
        description = "Generates Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)

        options {
            links("http://docs.oracle.com/javase/jp/7/api/");
            linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference")
            setMemberLevel(JavadocMemberLevel.PUBLIC)
            docEncoding = 'UTF-8'
            encoding = 'UTF-8'
            charSet = 'UTF-8'
        }

        exclude '**/org/deviceconnect/android/logger/**'
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
    }
}

def repo = new File(rootDir, "repository")

apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository url: "file://${repo.absolutePath}"
            pom.version = '1.0.1'
            pom.groupId = 'org.deviceconnect'
            pom.artifactId = 'dconnect-sdk-for-android'
        }
    }
}