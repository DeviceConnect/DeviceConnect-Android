apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    def getVersionName = { ->
        def version
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            version = ["cmd", "/c", "git describe --tags --long"].execute().in.text.trim()
        } else {
            version = ["sh", "-c", "git describe --tags --long"].execute().in.text.trim()
        }
        version = version.empty ? "2.1.0" : version
        return "${version}"
    }

    defaultConfig {
        applicationId "app.android.deviceconnect.org.simplebot"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName getVersionName()
        manifestPlaceholders = [
                appIcon: "@mipmap/dconnect_icon",
                appName: "@string/app_name"
        ]
    }

    signingConfigs {
        releaseConfig {
            storeFile=file(project.properties.storeFile)
            storePassword=project.properties.storePassword
            keyAlias=project.properties.keyAlias
            keyPassword=project.properties.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }
    productFlavors {
        DeviceWebApi {
            versionCode 1
            versionName "1.0.0"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    appName: "@string/app_name_for_device_web_api"
            ]
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(path: ':dconnect-sdk-for-android')
    compile 'com.x5dev:chunk-templates:3.2.2'
    compile 'org.cheffo:jeplite:0.8.7a'
    compile 'net.sf.opencsv:opencsv:2.3'
}
