apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    def getVersionName = { ->
        def version
        if (System.getProperty('os.name').toLowerCase().contains('windows')) {
            version = ["cmd", "/c", "git describe --tags --long"].execute().in.text.trim()
        } else {
            version = ["sh", "-c", "git describe --tags --long"].execute().in.text.trim()
        }
        version = version.empty ? "2.1.0" : version
        return "${version}"
    }

    defaultConfig {
        applicationId "org.deviceconnect.android.deviceplugin.fabo"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName getVersionName()
        manifestPlaceholders = [
                appIcon: "@mipmap/dconnect_icon",
                appName: "@string/app_name"
        ]
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseConfig {
            storeFile=file(project.properties.storeFile)
            storePassword=project.properties.storePassword
            keyAlias=project.properties.keyAlias
            keyPassword=project.properties.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }
    flavorDimensions  "DeviceWebApi","DeviceConnect"
    productFlavors {
        DeviceConnect {
            dimension 'DeviceConnect'
            versionCode 2
            versionName "1.1.0"
            manifestPlaceholders = [
                    appIcon: "@mipmap/dconnect_icon",
                    appName: "@string/app_name"
            ]
        }

        DeviceWebApi {
            dimension 'DeviceWebApi'
            versionCode 2
            versionName "1.1.0"
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    appName: "@string/app_name_for_device_web_api"
            ]
        }
    }
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

repositories {
    maven { url 'https://raw.githubusercontent.com/DeviceConnect/DeviceConnect-Android/master/dConnectSDK/dConnectSDKForAndroid/repository/' }
    maven { url 'https://raw.githubusercontent.com/DeviceConnect/DeviceConnect-Android/master/dConnectDevicePlugin/dConnectDevicePluginSDK/repository/' }
    maven { url "https://raw.githubusercontent.com/FaBoPlatform/FaBo-Serial-Kit/master/repository/" }
    maven { url 'https://raw.githubusercontent.com/FaBoPlatform/STK500-Android/master/stk500/repository/' }
    maven { url 'file://' + rootDir + '/../../dConnectSDK/dConnectSDKForAndroid/repository/' }
    maven { url 'file://' + rootDir + '/../../dConnectDevicePlugin/dConnectDevicePluginSDK/repository/' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile project(':app')
    compile 'io.fabo:serialkit:0.9.5'
    compile 'io.fabo.android.stk500:stk500-aar:1.0.4'
}
